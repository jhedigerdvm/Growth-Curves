
model{
  
#priors
  
  ageclass.beta[1] <- 0      #need to set baseline of zero for >1 categorical covariate
  bs.beta[1] <- 0     #need to set baseline of zero for >1 categorical covariate
  
  int~dunif(0,1000)        #need an intercept to build priors upon

  for (u in 2:3){
    bs.beta[u] ~ dnorm(0,0.001) #needed dnorm for bs because of negative values! 
  }
  
  for(u in 2:12){
    ageclass.beta[u] ~ dunif(0,1000)
  }
  
  tau <- 1/(sigma*sigma)
  sigma ~ dunif(0,100)


  for(capyear in 1:14){                    #random effect for capyear 
    eps.capyear[capyear] ~ dnorm(0, tau.capyear)
  }
  tau.capyear <- 1/(sigma.capyear * sigma.capyear)
  sigma.capyear ~ dunif(0, 100) #random effect SD youre saying that each year-specific error term is coming
                                # from the same distributionm i.e. same mean (0) and standard
                                # deviation (sigma.period). The standard deviation is what connects all the
                                # year-specific random effects to the same distribution.

  for(birthyear in 1:14){                    #random effect for birthyear
      eps.birthyear[birthyear] ~ dnorm(0, tau.birthyear)
    }
  tau.birthyear <- 1/(sigma.birthyear * sigma.birthyear)
  sigma.birthyear ~ dunif(0, 100)
  

  for(animal_id in 1:475){                       #random effect for animal_id
    eps.id[animal_id] ~ dnorm(0, tau.id)
  }

  tau.id <- 1/(sigma.id * sigma.id)
  sigma.id ~ dunif(0, 100)

# Likelihood 
for (i in 1:n){
   antler[i] ~ dnorm(mu[i], tau) #each antler is a draw from this distribution
   mu[i] <- int + bs.beta[bs[i]] + (ageclass.beta[ageclass[i]]) + eps.capyear[capyear[i]] + eps.birthyear[birthyear[i]] + eps.id[animal_id[i]]
  }

#derived parameter
for (i in 1:3){ #birthsite
  for (j in 1:12){ #age
    derived_antlers[i,j]<- int+ bs.beta[i] + ageclass.beta[j]
}
}

for(i in 1:2){
   site.diff[i] <- bs.beta[3] - bs.beta[i]
  }
}

